# 保护模式
## WHY
- 实模式下操作系统和用户程序属于同一特权级
- 用户程序所引用的地址都指向物理地址
- 用户程序可以自由修改段基址
导致用户程序乃至操作系统的数据都可以被随意地删改
- 最大可用内存 1M 过小
## 寄存器扩展
![](OS_LAB/attachments/Pasted%20image%2020230321151400.png)
### 段描述符缓冲寄存器（Descriptor Cache Registers）
CPU 访问内存获得段描述符信息后，整理成内存段存入 DCR。每次访问相同的段时，直接读取该段寄存器对应的段描述符缓冲寄存器。往段寄存器中赋值，CPU 就会更新段描述符缓冲寄存器。
![](OS_LAB/attachments/Pasted%20image%2020230321151945.png)
80386 地址总线和寄存器都是 32 位，任意一个段都可以访问到 4GB 空间

## 寻址扩展
保护模式下基址寄存器可以是所有的 32 位通用寄存器，偏移量可以是 32 位，还可以对变址寄存器乘上一个比例因子。
```
mov eax, [eax+edx*8+0x12345678]
mov eax, [eax+edx*2+0x8]
mov eax, [ecx*4+0x1234]
```
![](OS_LAB/attachments/Pasted%20image%2020230321155659.png)

## 运行模式反转
CPU可以同时支持16位、32位指令，编译器根据伪指令 bits 来编译成不同格式的机器代码。
bits 的指令格式是 \[bits 16\] 或 \[bits 32\]，不使用时默认为16位。bits 指令的范围是从当前 bits 标签直到下一个 bits 标签的范围。
- 反转前缀 0x66
![](OS_LAB/attachments/Pasted%20image%2020230322135906.png)
- 寻址方式反转前缀 0x67
![](OS_LAB/attachments/Pasted%20image%2020230322140046.png)
## 指令扩展
add、sub、mul、imul、div 对 32 位寄存器的支持
- 无符号数乘法 mul
如果乘数是 8 位，则把寄存器 al 当作另一个乘数，结果便是 16 位，存入寄存器 ax。 
如果乘数是 16 位，则把寄存器 ax 当作另一个乘数，结果便是 32 位，存入寄存器 eax。 
如果乘数是 32 位，则把寄存器 eax 当作另一个乘数，结果便是 64 位，存入 edx：eax，其中 edx 是积的高 32 位，eax 是积的低 32 位。
- 无符号数除法 div
如果除数是 8 位，被除数就是 16 位，位于寄存器 ax。商在寄存器 al，余数在寄存器 ah。 
如果除数是 16 位，被除数的高 16 位则位于寄存器 dx，被除数的低 16 位则位于寄存器 ax。商在寄存器 ax，余数在寄存器 dx。 
如果除数是 32 位，被除数的高 32 位则位于寄存器 edx，被除数的低 32 位则位于寄存器 eax，商在寄存器 eax，余数在寄存器 edx。

- push 指令
立即数可以压入8 位、16 位、32 位数据。
当压入 8 位立即数时，由于实模式下默认操作数是 16 位，CPU 会将其扩展为 16 位后再将其入栈，sp-2。
当压入 16 位立即数时，CPU 会将其直接入栈，sp-2。 
当压入 32 位立即数时，CPU 会将其直接入栈，sp-4。

段寄存器的入栈，即 cs、ds、es、fs、gs、ss，无论在哪种模式下，都是按当前模式的默认操作数大小压入的。

对于通用寄存器和内存，无论是在实模式或保护模式：
如果压入的是 16 位数据，栈指针减 2。 
如果压入的是 32 位数据，栈指针减 4。

# 全局描述符表 Global Descriptor Table
## 段描述符
增加一些属性描述内存段：特权级、段基址、段界限...
段描述符用来描述一个内存段，8Bytes
![](OS_LAB/attachments/Pasted%20image%2020230322145258.png)
段基址需要用 32 位地址来表示。
段界限表示段边界的扩展最值，用来限制段内偏移地址的，段内偏移地址必须位于段的范围之内，否则 CPU 会抛异常。描述符中的 G 位来指定单位是字节还是 4KB。故段的大小要么是 1MB，要么是 4GB。
扩展方向有上下两种。对于数据段和代码段，段的扩展方向是向上，此时的段界限用来表示段内偏移的最大值。对于栈段，段的扩展方向是向下，此时的段界限用来表示段内偏移的最小值。
实际的段界限边界值= （描述符中段界限+1）\*（段界限的粒度大小）- 1
S 字段指示是否是系统段，为 0 时表示系统段，为 1 时表示数据段，只有 S 字段的值确定后，type 字段的值才有具体意义。
type 字段共 4 位，用来指定本描述符的类型。
![](OS_LAB/attachments/Pasted%20image%2020230322151117.png)
![](OS_LAB/attachments/Pasted%20image%2020230322151138.png)
先关注非系统段
A：Accessed
每当该段被 CPU 访问过后，CPU 就将此位置 1。 创建一个新段描述符时，应该将此位置 0。
C：Conforming
如果自己是转移的目标段， 并且自己是一致性代码段，自己的特权级一定要高于当前特权级。
R：Readable
一般用来限制代码段的访问。如果指令执行过程中，CPU 发现某些指令对 R 为 0 的段进行访问，如使用段超越前缀 CS 来访问代码段，CPU 将抛出异常。
X：EXecutable
代码段是可执行的，X 为 1。而数据段是不可执行的，X 为 0。
E：Extend
E 为 0 表示向上扩展，通常用于代码段和数 据段。E 为 1 表示向下扩展，通常用于栈段。
W：Writable
W 为 1 表示可写，通常用于数据段。W 为 0 表示不可写入，通常用于代码段。对于 W 为 0 的段有写入行为，同样会引发 CPU 抛出异常。

P 字段（Present）
如果段存在于内存中，P 为 1，否则 P 为 0。
P 字段是由 CPU 来检查的，如果为 0 将抛出异常。

AVL 字段
操作系统可以随意用此位，对硬件来说没有专门的用途。

L 字段
设置是否是 64 位代码段。L 为 1 表示 64 位代码段，否则表示 32 位代码段。

DPL 字段，Descriptor Privilege Level，即描述符特权级。

D/B 字段
指示有效地址（段内偏移地址）及操作数的大小。为了兼容 286 的保护模式（操作数 16 位）。
对于代码段来说是 D 位，若 D 为 0，表示指令中的有效地址和操作数是 16 位，指令有效地址用 IP 寄存器。若 D 为 1，表示指令中的有效地址及操作数是 32 位，指令有效地址用 EIP 寄存器。
对于栈段来说，此位是 B 位，用来指定操作数大小，此操作数涉及到栈指针寄存器的选择及栈的地址上限。若 B 为 0，使用的是 sp 寄存器，也就是栈的起始地址是 16 位寄存器的最大寻址范围，0xFFFF。 若 B 为 1，使用的是 esp 寄存器，也就是栈的起始地址是 32 位寄存器的最大寻址范围，0xFFFFFFFF。

## GDTR
48 位寄存器，专门用来存储 GDT 的内存地址及大小。
0-15 位为界限，15-47 位为GDT内存起始地址
- 访问该寄存器使用 lgdt 指令
GDT 中最多 8192 个段或门

## 选择子
段寄存器在实模式存段基址，保护模式因为已经在 GDT 中存了基址，段寄存器存入的是选择子，在 GDT 中索引段描述符。
0-1 位存 RPL，表示 0、1、2、3 四种特权级。
2 位存 Table Indicator，用来指示选择子是在 GDT 还是 LDT 中索引描述符。为 0 表示在 GDT 中索引。
剩下 13 位索引部分，对应最多 8192 个段。

## LDT
每个任务都有自己的 LDT，随着任务切换要切换相应任务的 LDT。
对应 LDTR，lldt 指令重新加载任务的私有内存段。
LDT 为系统段，需要一个描述符在 GDT 中注册。
lldt 指令格式：lldt 16 位寄存器或16 位内存，内容是一个选择子。
与 GDT 不同的是 LDT 中的第 0 个段描述符是可用的。

# A20 Gate
如果 A20Gate 被打开，访问到 0x100000～0x10FFEF 之间的地址时，CPU 将真正访问这块物理内存。
如果 A20Gate 被禁止，访问 0x100000～0x10FFEF 之间的地址时，CPU 将采用 8086/8088 的地址[回绕](OS_LAB/1%20启动过程&MBR编写.md#实模式下内存分段)。
将端口 0x92 的第 1 位置为 1
```
in al，0x92 
or al，0000_0010B 
out 0x92，al
```

# CR0 Register
CR0 寄存器的第 0 位，Protection Enable
PE 为 0 表示在实模式下运行，PE 为 1 表示在保护模式下运行。
```
mov eax, cr0
or eax, 0x00000001
mov cr0, eax
```

# 进入保护模式
- 新的 loader 更大，修改 mbr 中读入的扇区数，变成读入 4 扇区。
- 修改 boot.inc 增加一些配置信息。
```
;loader & kernel
LOADER_BASE_ADDR equ 0x900
LOADER_START_SECTOR equ 0x2

; Descriptor attributes
DESC_G_4K equ 10000000_00000000_00000000b   ; G 字段为 1
DESC_D_32 equ 1000000_00000000_00000000b    ; D/B 字段为 1
DESC_L equ 000000_00000000_00000000b    ; L 字段为 0
DESC_AVL equ 00000_00000000_00000000b   ; AVL 字段为 0
DESC_LIMIT_CODE2 equ 1111_00000000_00000000b    ; 代码段段界限第二部分
DESC_LIMIT_DATA2 equ DESC_LIMIT_CODE2   ; 数据段段界限第二部分
DESC_LIMIT_VIDEO2 equ 0000_0000000000000000b ; 显存段段界限第二部分
DESC_P equ 1_000000000000000b   ; Present, 段存在于内存中
DESC_DPL_0 equ 00_0000000000000b    ; 最高特权级
DESC_DPL_1 equ 01_0000000000000b    
DESC_DPL_2 equ 10_0000000000000b
DESC_DPL_3 equ 11_0000000000000b    ; 最低特权级
DESC_S_CODE equ 1_000000000000b ; 非系统段
DESC_S_DATA equ DESC_S_CODE ; 同上
DESC_S_sys equ 0_000000000000b  ; 系统段
DESC_TYPE_CODE equ 1000_00000000b   ; 只执行的代码段
DESC_TYPE_DATA equ 0010_00000000b   ; 只读，向下扩展的数据段

DESC_CODE_HIGH4B equ (0x00 << 24) + DESC_G_4K + DESC_D_32 + DESC_L + \
DESC_AVL + DESC_LIMIT_CODE2 + DESC_P + DESC_DPL_0 + DESC_S_CODE + \
DESC_TYPE_CODE + 0x00

DESC_DATA_HIGH4B equ (0X00 << 24) + DESC_G_4K + DESC_D_32 + DESC_L + \
DESC_AVL + DESC_LIMIT_DATA2 + DESC_P + DESC_DPL_0 + DESC_S_DATA + \
DESC_TYPE_DATA + 0x00

DESC_VIDEO_HIGH4B equ (0X00 << 24) + DESC_G_4K + DESC_D_32 + DESC_L + \
DESC_AVL + DESC_LIMIT_VIDEO2 + DESC_P + DESC_DPL_0 + DESC_S_DATA + \
DESC_TYPE_DATA + 0x0B

; Selector attributes
RPL0 equ 00b
RPL1 equ 01b
RPL2 equ 10b
RPL3 equ 11b
TI_GDT equ 000b
TI_LDT equ 100b
```

```
%include "boot.inc"
section loader vstart=LOADER_BASE_ADDR
LOADER_STACK_TOP equ LOADER_BASE_ADDR
jmp loader_start

; 构建 GDT 和内部描述符
GDT_BASE:   dd 0x00000000   ; 第 0 个为空
            dd 0x00000000
CODE_DESC:  dd 0x0000FFFF   ; 基址 0，limit 全 1
            dd DESC_CODE_HIGH4B ; 定义的代码段高 4B 
DATA_STACK_DESC:    dd 0x0000FFFF
                    dd DESC_DATA_HIGH4B ; 数据段高 4B
VIDEO_DESC: dd 0x80000007
            dd DESC_VIDEO_HIGH4B
; 和高四位拼出段基址 0xb8000，limit = (0xBFFFF - 0xB8000) / 4K = 0x7
GDT_SIZE equ $ - GDT_BASE
GDT_LIMIT equ GDT_SIZE - 1
times 60 dq 0
SELECTOR_CODE equ (0x0001 << 3) + TI_GDT + RPL0
SELECTOR_DATA equ (0x0002 << 3) + TI_GDT + RPL0
SELECTOR_VIDEO equ (0x0003 << 3) + TI_GDT + RPL0

gdt_ptr dw GDT_LIMIT
        dd GDT_BASE

msg db 'loader in real mode'

loader_start:
    mov sp, LOADER_BASE_ADDR
    mov bp, msg
    mov cx, 19 ; 串长度
    mov ax,0x1301 ;a h为子功能13，al设置写字符方式01
    mov bx,0x00F4 ; bh为页号，bl为属性
    mov dx, 0x1800  ; 位置左下角
    int 0x10 ; 写字符串

    ; 进入保护模式
    in al, 0x92
    or al, 0x02
    out 0x92, al

    lgdt [gdt_ptr]

    mov eax, cr0
    or eax, 0x00000001
    mov cr0, eax

    jmp dword SELECTOR_CODE:p_mode_start    ; 刷新流水线

[bits 32]
p_mode_start:
    mov ax, SELECTOR_DATA
    mov ds, ax
    mov es, ax
    mov ss, ax
    mov esp, LOADER_STACK_TOP
    mov ax, SELECTOR_VIDEO
    mov gs, ax
    mov byte [gs:160], 'H'
    mov byte [gs:162], 'E'
    mov byte [gs:164], 'L'
    mov byte [gs:166], 'L'
    mov byte [gs:168], 'O'
    
    jmp $


```
# 平坦模型
分段机制作为 X86 处理器的固有机制，无法绕过。处理器总是按照段基址 + 段内偏移的方式生成线性地址。
平坦模型下的内存管理：
- 在程序中实质上不分段，实际上就是只保留一个段，代码和数据都在这个段内
	- 此时程序中分段的作用只是一个容器，用于在逻辑上组织不同功能的代码，而不具备内存管理上的意义
	- 在加载程序时，不会为这些程序中的段构建段描述符
- 系统中仍然需要构造段描述符，但是每个段的线性基地址均为 0，段界限均为 0xFFFFF，段粒度均为 4KB，也就是说每个段描述符都指向完整的 4GB 线性地址空间
	- 所有段描述符描述的段都是 4GB
	- 至少要构建数据段描述符和代码段描述符，都是 4GB 但类型不同
	- 段机制的特权级保护仍生效，界限检查失效
- 在程序加载时，将程序一次性加载到线性地址空间中，同时创建页目录和页表，建立线性地址空间到物理内存的映射

# 内存段保护
## 段寄存器加载选择子
首先根据选择子的值验证段描述符是否超越界限。
检查段寄存器的用途和段类型是否匹配
- 只有具备可执行属性的段（代码段）才能加载到 CS 段寄存器中。
- 只具备执行属性的段（代码段）不允许加载到除 CS 外的段寄存器中。
- 只有具备可写属性的段（数据段）才能加载到 SS 栈段寄存器中。
- 至少具备可读属性的段才能加载到 DS、ES、FS、GS 段寄存器中。

## 代码段和数据段
偏移地址+数据长度-1≤实际段界限大小

## 栈段
- 对于向上扩展的段，实际的段界限是段内可以访问的最后一字节。
- 对于向下扩展的段，实际的段界限是段内不可以访问的第一个字节。